plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.github.ben-manes.versions' version '0.52.0'
}

ext {
	springAiVersion = "1.0.0-M6"
}

group = 'gettothepoint'
if (!project.hasProperty('version')) {
	throw new GradleException("Missing version property in gradle.properties")
}
version = project.version

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	google()
}

dependencies {
	// ===== Spring Boot Core =====
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

	// ===== Database Driver =====
	runtimeOnly 'org.postgresql:postgresql'

	// ===== Security & Authentication =====
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

	// ===== API Documentation =====
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'

	// ===== Lombok =====
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// ===== Testing =====
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.assertj:assertj-core:4.0.0-M1'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.12.1'
	testImplementation 'com.h2database:h2'

	// ===== Youtube  =====
	implementation 'com.google.api-client:google-api-client:2.7.2'
	implementation 'com.google.apis:google-api-services-youtube:v3-rev20250224-2.0.0'
	implementation 'com.google.apis:google-api-services-youtubeAnalytics:v2-rev20230704-2.0.0'
	implementation 'com.google.auth:google-auth-library-oauth2-http:1.33.1'

	// ===== TTS =====
	implementation 'com.google.cloud:google-cloud-texttospeech:2.60.0'

	// ===== AI =====
	implementation platform("org.springframework.ai:spring-ai-bom:1.0.0-M6")
	implementation 'org.springframework.ai:spring-ai-openai-spring-boot-starter'
	implementation 'org.springframework.ai:spring-ai-openai'


}

tasks.named('test') {
	useJUnitPlatform()
}